apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'org.jetbrains.dokka-android'


static def getAppVersionCode() {
    return 3
}

static def getAppVersionName() {
    return "0.2.2"
}

group = 'com.github.valartech'
version = getAppVersionName()

android {
    compileSdkVersion 28


    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode getAppVersionCode()
        versionName getAppVersionName()

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        androidTest.resources.srcDirs += 'src/androidTest/res'
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    //android
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-reactivestreams:$lifecycle_version"

    //kotlin
    implementation "androidx.core:core-ktx:1.1.0-alpha04"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"
    implementation "org.jetbrains.anko:anko-sdk25:$anko_version"

    //dagger
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    //utilities
    implementation "com.jakewharton.timber:timber:4.7.1"
    implementation "ru.terrakok.cicerone:cicerone:5.0.0"
    debugImplementation "com.ashokvarma.android:gander:${gander_version}"
    releaseImplementation "com.ashokvarma.android:gander-no-op:${gander_version}"
    implementation "com.xwray:groupie:${groupie_version}"
    implementation "com.xwray:groupie-kotlin-android-extensions:${groupie_version}"
    implementation "com.jakewharton.threetenabp:threetenabp:1.1.1"
    implementation "com.crashlytics.sdk.android:crashlytics:2.9.9"
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

//task javadoc(type: Javadoc) {
//    failOnError false
//    source = android.sourceSets.main.java.source
//    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
//    classpath += configurations.compile
//}
//
//// build a jar with javadoc
//task javadocJar(type: Jar, dependsOn: javadoc) {
//    classifier = 'javadoc'
//    from javadoc.destinationDir
//}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from "$buildDir/javadoc"
}


task classesJar(type: Jar) {
    from "$buildDir/intermediates/classes/release"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.source
}

artifacts {
    archives classesJar
    archives sourcesJar
    archives javadocJar
}
